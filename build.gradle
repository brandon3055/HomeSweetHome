buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'maven'
apply plugin: 'maven-publish'

group = "com.brandon3055"
archivesBaseName = "HomeSweetHome"

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

// Define properties file
ext.configFile = file "build.properties"

configFile.withReader {
    // Load config.  It shall from now be referenced as simply config or project.config
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}

configurations {
    pack
    compile {
        extendsFrom pack
    }
}

repositories {
    maven {
        name = "CoFH Maven"
        url = "http://maven.covers1624.net"
    }
    maven {
        name 'CB Maven FS'
        url "http://chickenbones.net/maven/"
    }
    maven {
        name "JEI Maven."
        url "http://dvs1.progwml6.com/files/maven"
    }
}

dependencies {
    compile "codechicken:CodeChickenLib:${config.ccl_mc_version}-${config.ccl_version}:deobf"
    compile "com.brandon3055.brandonscore:BrandonsCore:${config.mc_version}-${config.bcore_version}:deobf"
    pack "codechicken:ChickenASM:${config.mc_version}-${config.cc_asm_version}"
}


version = "${project.config.mod_version}." + (System.getenv("BUILD_NUMBER") ?: "21")
println config.mc_version + "-" + config.forge_version
minecraft {
    version = config.mc_version + "-" + config.forge_version
    runDir = "run"
    mappings = "${config.mappings}"
    replace '${mod_version}', project.config.mod_version
    makeObfSourceJar = false
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mc_version", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mc_version
        expand 'version': project.version, 'mc_version': project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
    rename '(.+_at.cfg)', 'META-INF/$1'
}

version = "${project.minecraft.version}-${project.version}"

def commonManifest = {
    attributes 'ContainedDeps': "ChickenASM-${config.mc_version}-${config.cc_asm_version}.jar ${archivesBaseName}-${version}-asm.jar"
    attributes 'FMLAT': 'HomeSweetHome_at.cfg'
}

def asmManifest = {
    attributes 'FMLCorePlugin': 'com.brandon3055.homesweethome.asm.LoadingPlugin'
}

//Because FG reobfuscation..
jar {
    classifier = 'temp'
}

task asmJar(type: Jar, dependsOn: reobfJar) {
    classifier = 'asm'
    manifest asmManifest
    from(zipTree(jar.archivePath)) {
        include "**/com/brandon3055/homesweethome/asm/*.class"
        include "**/assets/homesweethome/asm/**"
    }
}

task universalJar(type: Jar, dependsOn: asmJar) {
    classifier = 'universal'
    manifest commonManifest
    from configurations.pack
    from asmJar.archivePath
    from(zipTree(jar.archivePath)) {
        exclude "**/com/brandon3055/homesweethome/asm/**"
    }
}

task srcJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
    manifest commonManifest
    from configurations.pack
    from asmJar.archivePath
}

// Tell the artifact system about our extra jars
artifacts {
    archives universalJar, asmJar, srcJar, deobfJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            if (project.hasProperty("local_maven")) {
                repository(url: "file://${local_maven}")
                pom {
                    groupId = project.group
                    version = project.version
                    artifactId = project.archivesBaseName
                    project {
                        name project.archivesBaseName
                        packaging 'jar'
                        description 'HomeSweetHome'
                        url 'https://github.com/brandon3055/HomeSweetHome'
                        scm {
                            url 'https://github.com/brandon3055/HomeSweetHome'
                            connection 'scm:git:git@github.com:brandon3055/HomeSweetHome.git'
                            developerConnection 'scm:git:git@github.com:brandon3055/HomeSweetHome.git'
                        }
                        issueManagement {
                            system 'github'
                            url 'https://github.com/brandon3055/HomeSweetHome/issues'
                        }
                        licenses {
                            license {
                                name 'License'
                                url 'https://raw.githubusercontent.com/brandon3055/HomeSweetHome/master/LICENSE.txt'
                                distribution 'repo'
                            }
                        }
                        developers {
                            developer {
                                id 'brandon3055'
                                name 'brandon3055'
                                roles { role 'developer' }
                            }
                        }
                    }
                }
            } else {
                println 'Maven upload triggered but no local_maven property found!'
            }
        }
    }
}
